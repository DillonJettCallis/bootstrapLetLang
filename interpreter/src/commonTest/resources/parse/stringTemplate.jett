import this/position.Position

import this/parse.parseAst.ParseCallExp
import this/parse.parseAst.ParseIdentifierExp
import this/parse.parseAst.ParseStringLiteralExp
import this/parse.parseAst.ParseCharLiteralExp
import this/parse.parseAst.ParseBinaryOpExp
import this/parse.parseAst.ParseExpression

import this/parse.parser.parseExpressionOnly

import this/parse.lexer.StringCursor
import this/parse.lexer.lexFragment
import this/parse.lexer.munchWord

data StringTemplateString { raw: String }
data StringTemplateChar { char: Char }
data StringTemplate { strings: List[String], values: List[ParseExpression] }

private enum StringTemplateResult {
  StringTemplateString,
  StringTemplateChar,
  StringTemplate,
}

fun parseStringTemplateWithPrefix(raw: String, pos: Position, prefix: String): ParseExpression = {
  val template = match (prefix) {
    "raw" => StringTemplateString { raw }
    "char" => {
      val cursor = StringCursor{raw, index: 0, pos}
      val (escapedCursor, escaped) = cursor.nextEscaped()

      if (escapedCursor.isNotEmpty()) {
        pos.fail("Expected char literal in char prefixed string")
      } else {
        StringTemplateChar{ char: escaped }
      }
    }
    _ => munchStringBody(StringCursor{raw, index: 0, pos}, List.of(""), List.of())
  }

  return match (template) {
    _ when template is StringTemplateString => ParseStringLiteralExp{value: template.raw, pos}
    _ when template is StringTemplateChar => ParseCharLiteralExp{value: template.char, pos}
    _ when template is StringTemplate => ParseCallExp {
         func: ParseIdentifierExp{name: "@template", pos},
         typeArguments: List.of(),
         arguments: List.of(
           listOfCall(template.strings.map({ value => ParseStringLiteralExp{value, pos} }), pos),
           listOfCall(template.values, pos)
         ),
         pos
       }
  }
}

private fun munchStringBody(cursor: StringCursor, initStrings: List[String], initValues: List[ParseExpression]): StringTemplateResult = {
  if (cursor.isEmpty()) {
    if (initValues.isEmpty()) {
      return StringTemplateString {raw: initStrings.last() }
    } else {
      return StringTemplate{strings: initStrings, values: initValues}
    }
  }

  val maybeDollar = cursor.curr()

  if (maybeDollar == char'\$') {
    val nextCursor = cursor.skip()

    if (nextCursor.isEmpty()) {
      cursor.pos.fail("Cannot end a string with \$")
    }

    val maybeBracket = nextCursor.curr()

    val (finalCursor, ex) = if (maybeBracket == char'{') {
      val (bodyCursor, body) = blockBody(nextCursor.skip(), "", List.of(char'{'))

      (bodyCursor, parseExpressionOnly(lexFragment(body, nextCursor.pos)))
    } else {
      val (wordCursor, word) = munchWord(nextCursor, "")

      (wordCursor, ParseIdentifierExp{name: word, pos: nextCursor.pos})
    };

    return munchStringBody(finalCursor, initStrings.add(""), initValues.add(ex))
  } else {
    val (nextCursor, nextChar) = cursor.nextEscaped()
    val stringsInit = initStrings.init()
    val working = initStrings.last()
    val appendedString = working.append(nextChar.toString())
    return munchStringBody(nextCursor, stringsInit.add(appendedString), initValues)
  }
}

private val quotes: String = "'\"`"

private fun blockBody(cursor: StringCursor, init: String, inside: List[Char]): (StringCursor, String) = {
  val (nextCursor, nextChar) = cursor.next()

  match (nextChar) {
    char'}' when inside.last() == char'{' && inside.size() == 1 => return (nextCursor, init)
    char'}' when inside.last() == char'{' && inside.size() > 1 => return blockBody(nextCursor, init.append(nextChar.toString()), inside.init())
    char'{' when inside.last() == char'{' => return blockBody(nextCursor, init.append(nextChar.toString()), inside.add(nextChar))
    _ when quotes.contains(nextChar) && inside.last() == nextChar => return blockBody(nextCursor, init.append(nextChar.toString()), inside.init())
    _ when quotes.contains(nextChar) && inside.last() != nextChar => return blockBody(nextCursor, init.append(nextChar.toString()), inside.add(nextChar))
    _ => return blockBody(nextCursor, init.append(nextChar.toString()), inside)
  }
}

fun listOfCall(arguments: List[ParseExpression], pos: Position): ParseCallExp = ParseCallExp {
  func: ParseBinaryOpExp{ op: '.', left: ParseIdentifierExp{ name: 'List', pos }, right: ParseIdentifierExp{ name: 'of', pos }, pos },
  arguments,
  typeArguments: List.of(),
  pos
}

